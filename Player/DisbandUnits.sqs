; args: [units]

_units = _this select 0

_groups = [groupPlayer]
? giPlayer == giCO select siPlayer : _groups = _groups + (groupAiMatrix select siPlayer)

; player globalchat format["_groups = %1", _groups]

_i = 0
_c = count _units
#CheckUnit
  ? !(_i < _c) : exit 
  _u = _units select _i; _i=_i+1
  
  ? !(alive _u) : player groupchat "You can't disband dead units."; goto "CheckUnit"

  _crew = crew _u
  ; player globalchat format["crew _u = %1", crew _u]
  
  ? giPlayer != (giCO select siPlayer) && count _crew == 0 : player groupchat "Only commanders can disband empty vehicles."; goto "CheckUnit"

  ; non-empty vehicle
  ? count _crew > 0 && !(_u in _crew) : player groupchat "You can't disband non-empty vehicles."; goto "CheckUnit"

  ; empty vehicle
  ? count _crew == 0 && (_u in mhq) : player groupchat "You can't disband MHQ."; goto "CheckUnit"
  ? count _crew == 0 : player groupchat "Disbanding vehicle."; goto "SkipKill"

  ; check if unit is a non-leader in allowed groups
  ? !((group _u) in _groups) : player groupchat "You are not allowed to disband this unit."; goto "CheckUnit"
  ? _u == leader _u : player groupchat "You can't disband group leaders."; goto "CheckUnit"

  ? _u == vehicle _u : player groupchat "Disbanding unit."; goto "SkipKill"
  player groupchat "Disbanding unit."
  _u setDamage 1
  ~1
  #SkipKill
  ~1
  deleteVehicle _u
  goto "CheckUnit"
